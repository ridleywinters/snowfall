[private]
default:
    @just --list --unsorted

# decompress external assets
expand:
    which ffmpeg
    test -d "./expanded/tiny_textures" || just expand-tiny-textures
    test -d "./expanded/raven_fantasy_icons_free" || just expand-raven_fantasy_icons_free
    test -d "./expanded/rpg_sound_pack" || just expand-rpg_sound_pack

[private]
expand-tiny-textures:
    mkdir -p ./expanded/tiny_textures/pack-1
    mkdir -p ./expanded/tiny_textures/pack-2
    mkdir -p ./expanded/tiny_textures/pack-3
    unzip "source/tiny_textures/SBS - Tiny Texture Pack - 512x512.zip" -d ./expanded/tiny_textures/pack-1
    unzip "source/tiny_textures/SBS - Tiny Texture Pack 2 - 512x512.zip" -d ./expanded/tiny_textures/pack-2
    unzip "source/tiny_textures/SBS - Tiny Texture Pack 3 - Large.zip" -d ./expanded/tiny_textures/pack-3
    cp "source/tiny_textures/attribution.meta.md" ./expanded/tiny_textures

# Extract just the 32x32 icons
[private]
expand-raven_fantasy_icons_free:
    mkdir -p ./expanded/raven_fantasy_icons_free
    unzip "source/raven_fantasy_icons_free/raven_fantasy_icons_free.zip" -d ./expanded/raven_fantasy_icons_free
    mv "./expanded/raven_fantasy_icons_free/Free - Raven Fantasy Icons/Separated Files/32x32/"* ./expanded/raven_fantasy_icons_free/
    rm -rf "./expanded/raven_fantasy_icons_free/Free - Raven Fantasy Icons"
    cp "source/raven_fantasy_icons_free/attribution.meta.md" ./expanded/raven_fantasy_icons_free

[private]
expand-rpg_sound_pack:
    mkdir -p ./expanded/rpg_sound_pack
    unzip "source/rpg_sound_pack/rpg_sound_pack.zip" -d ./expanded/rpg_sound_pack
    cp -f "source/rpg_sound_pack/attribution.meta.md" ./expanded/rpg_sound_pack
    rm -rf ./expanded/rpg_sound_pack/__MACOSX
    mv "./expanded/rpg_sound_pack/RPG Sound Pack/"* ./expanded/rpg_sound_pack
    rm -rf "./expanded/rpg_sound_pack/RPG Sound Pack"
    find . -type f -iname "*.wav" -print0 | while IFS= read -r -d '' f; do \
        echo "Converting $f..."; \
        ffmpeg -loglevel error -y -i "$f" -c:a libvorbis -q:a 5 "${f%.wav}.ogg"; \
        rm "$f"; \
    done

clean:
    git clean -fdX ./expanded
