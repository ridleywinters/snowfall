[private]
default:
    @just --list --unsorted    

ensure:
    mkdir -p ./base/textures
    mkdir -p ./base/sprites
    mkdir -p ./base/icons

build:
    @just run_with_sentinel _build-imp ./base/.build-complete

_build-imp: ensure
    cd ${REPO_ROOT}/extern && just expand
    build/build-tiny-textures.ts
    build/build-content.ts
    build/build-raven-icons.ts
    build/build-items.ts

rebuild: clean build

clean:
    git clean -fdX ./base

#==============================================================================
# Utility recipes
#==============================================================================

# Runs the target recipe if and only if the specified filename does not exist
[private]
run_if_not_exists target filename:
    #!/usr/bin/env bash
    if [ ! -f {{filename}} ]; then \
        just {{target}}; \
    fi

[private]
run_with_sentinel target sentinel:
    #!/usr/bin/env bash
    if [ ! -f {{sentinel}} ]; then \
        just {{target}}; \
        touch {{sentinel}}; \
    fi
